// This Source Code Form is subject to the terms of the Mozilla Public
// License, v. 2.0. If a copy of the MPL was not distributed with this
// file, You can obtain one at https://mozilla.org/MPL/2.0/.
Перем юТест;

Функция ПолучитьСписокТестов(ЮнитТестирование) Экспорт
	юТест = ЮнитТестирование;

	ВсеТесты = Новый Массив;
	ВсеТесты.Добавить("ТестДолжен_ПроверитьОстатокОтДеления");
	ВсеТесты.Добавить("ТестДолжен_ПроверитьЧтоДелениеНаНольВыдетОшибку");
	ВсеТесты.Добавить("ТестДолжен_ПроверитьСложениеДатыИЧисла");
	ВсеТесты.Добавить("ТестДолжен_ПроверитьВычитаниеИзДатыЧисла");
	Возврат ВсеТесты;
КонецФункции

Процедура ТестДолжен_ПроверитьОстатокОтДеления() Экспорт

	юТест.ПроверитьРавенство(3 % 1, 0, "3 % 1 == 0");
	юТест.ПроверитьРавенство(3.2 % 1, 0.2, "3.2 % 1 == 0.2");
	юТест.ПроверитьРавенство(-3.2 % 1, -0.2, "-3.2 % 1 == -0.2");
	юТест.ПроверитьРавенство(-3.2 % -1, -0.2, "-3.2 % -1 == 0.2");

КонецПроцедуры

Процедура ТестДолжен_ПроверитьЧтоДелениеНаНольВыдетОшибку() Экспорт

    БылоИсключение = Ложь;
    Попытка
        Ф = 1 % 0;
    Исключение
        БылоИсключение = Истина;
    КонецПопытки;

    юТест.ПроверитьРавенство(БылоИсключение, Истина, "Хотели исключение, а его не было");

КонецПроцедуры

Процедура ТестДолжен_ПроверитьСложениеДатыИЧисла() Экспорт

	ИсходнаяДата = '20221226140600';
	юТест.ПроверитьРавенство(ИсходнаяДата + 1, '20221226140601', "Плюс одна секунда");
	юТест.ПроверитьРавенство(ИсходнаяДата + 60, '20221226140700', "Плюс 60 секунд");
	юТест.ПроверитьНеРавенство(ИсходнаяДата + 0.1, ИсходнаяДата, "Плюс 0.1 секунда прибавилась");
	юТест.ПроверитьБольше(ИсходнаяДата + 0.1, ИсходнаяДата, "Плюс 0.1 секунда увеличила");
	юТест.ПроверитьМеньше(ИсходнаяДата + 0.1, ИсходнаяДата + 1, "Плюс 0.1 секунда увеличила на чуть-чуть");

КонецПроцедуры

Процедура ТестДолжен_ПроверитьВычитаниеИзДатыЧисла() Экспорт

	ИсходнаяДата = '20221226140600';
	юТест.ПроверитьРавенство(ИсходнаяДата - 1, '20221226140559', "Минус одна секунда");
	юТест.ПроверитьРавенство(ИсходнаяДата - 60, '20221226140500', "Минус 60 секунд");
	юТест.ПроверитьНеРавенство(ИсходнаяДата - 0.1, ИсходнаяДата, "Минус 0.1 секунда убавилась");
	юТест.ПроверитьМеньше(ИсходнаяДата - 0.1, ИсходнаяДата, "Минус 0.1 секунда уменьшила");
	юТест.ПроверитьБольше(ИсходнаяДата - 0.1, ИсходнаяДата - 1, "Минус 0.1 секунда уменьшила на чуть-чуть");

КонецПроцедуры
