Перем ЮнитТестирование;

Функция ПолучитьСписокТестов(Тестировщик) Экспорт
	ЮнитТестирование = Тестировщик;

	ВсеТесты = Новый Массив;
	ВсеТесты.Добавить("ТестДолжен_ПроверитьТекущуюДату");
	ВсеТесты.Добавить("ТестДолжен_ПроверитьЧтоНулиЭтоПустаяДата");
	ВсеТесты.Добавить("ТестДолжен_ПроверитьСложениеДатыИЧисла");
	ВсеТесты.Добавить("ТестДолжен_ПроверитьВычитаниеИзДатыЧисла");
	Возврат ВсеТесты;
КонецФункции

Процедура ТестДолжен_ПроверитьТекущуюДату() Экспорт

    Дата1 = ТекущаяДата();

    ЮнитТестирование.ПроверитьРавенство(ТипЗнч(Дата1), Тип("Дата"), "Текущая дата - это дата");

    Дата2 = ТекущаяДата();
    ЮнитТестирование.Проверить(Дата1 <= Дата2, "ТекущаяДата() не убывает");

КонецПроцедуры

Процедура ТестДолжен_ПроверитьЧтоНулиЭтоПустаяДата() Экспорт

    ЮнитТестирование.ПроверитьРавенство('00000000',       '00010101', "00000000 - это 1 января 1 года");
    ЮнитТестирование.ПроверитьРавенство('000000000000',   '00010101', "0000000000 - это 1 января 1 года");
    ЮнитТестирование.ПроверитьРавенство('00000000000000', '00010101', "00000000000000 - это 1 января 1 года");

КонецПроцедуры


Процедура ТестДолжен_ПроверитьСложениеДатыИЧисла() Экспорт

	ИсходнаяДата = '20221226140600';
	ЮнитТестирование.ПроверитьРавенство(ИсходнаяДата + 1, '20221226140601', "Плюс одна секунда");
	ЮнитТестирование.ПроверитьРавенство(ИсходнаяДата + 60, '20221226140700', "Плюс 60 секунд");
	ЮнитТестирование.ПроверитьНеРавенство(ИсходнаяДата + 0.1, ИсходнаяДата, "Плюс 0.1 секунда прибавилась");
	ЮнитТестирование.ПроверитьБольше(ИсходнаяДата + 0.1, ИсходнаяДата, "Плюс 0.1 секунда увеличила");
	ЮнитТестирование.ПроверитьМеньше(ИсходнаяДата + 0.1, ИсходнаяДата + 1, "Плюс 0.1 секунда увеличила на чуть-чуть");

КонецПроцедуры

Процедура ТестДолжен_ПроверитьВычитаниеИзДатыЧисла() Экспорт

	ИсходнаяДата = '20221226140600';
	ЮнитТестирование.ПроверитьРавенство(ИсходнаяДата - 1, '20221226140559', "Минус одна секунда");
	ЮнитТестирование.ПроверитьРавенство(ИсходнаяДата - 60, '20221226140500', "Минус 60 секунд");
	ЮнитТестирование.ПроверитьНеРавенство(ИсходнаяДата - 0.1, ИсходнаяДата, "Минус 0.1 секунда убавилась");
	ЮнитТестирование.ПроверитьМеньше(ИсходнаяДата - 0.1, ИсходнаяДата, "Минус 0.1 секунда уменьшила");
	ЮнитТестирование.ПроверитьБольше(ИсходнаяДата - 0.1, ИсходнаяДата - 1, "Минус 0.1 секунда уменьшила на чуть-чуть");

	ПустаяДата = '00010101';
	ЮнитТестирование.ПроверитьРавенство(Дата(1,1,1) - 1, ПустаяДата, "Вычитание лишнего 1");
	ЮнитТестирование.ПроверитьРавенство(Дата(1,1,1, 0, 0, 5) - 10, ПустаяДата, "Вычитание лишнего 2");

КонецПроцедуры
